      SUBROUTINE SUBFLD(BIGFLD,IMXBIG,JMXBIG,SMLFLD,IMXSML,JMXSML,
     1           ICORNR,JCORNR,AMULT1,ADD1,AMULT2,ADD2,IEXIT)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SUBFLD      TO EXTRACT FROM THE GIVEN ARRAY OF GRID
C   PRGMMR: KRISHNA KUMAR         ORG: W/NP12   DATE: 1999-08-01
C
C ABSTRACT: TO EXTRACT FROM THE GIVEN 2-DIMENSIONAL ARRAY OF
C   GRIDPOINT DATA A SMALLER ARRAY OF GRIDPOINT DATA AND, AT THE
C   SAME TIME, TO RESCALE THE GRIDPOINT VALUES USING THE FOLLOWING
C   FORMULA --
C       RESULT = (ORIGINAL*AMULT1 + ADD1)*AMULT2 + ADD2
C
C PROGRAM HISTORY LOG:
C   82-05-13  DAVE S.
C   89-10-17  LUKE LIN    ADD DOCBLOCK.
C   94-12-30  LUKE LIN    CONVERT IT CFT-77.
C 1999-08-01  KRISHNA KUMAR CONVERTED THIS CODE FROM CRAY TO IBM RS/6000.
C
C USAGE:    CALL SUBFLD(BIGFLD,IMXBIG,JMXBIG,SMLFLD,IMXSML,JMXSML,
C                  ICORNR,JCORNR,AMULT1,ADD1,AMULT2,ADD2,IEXIT)
C   INPUT ARGUMENTS:
C     BIGFLD   - REAL*4  BIGFLD(IMXBIG,JMXBIG)
C                THE ORIGINAL 2-DIMENSIONAL ARRAY OF GRIDPOINT DATA.
C     IMXBIG   - I-DIMENSION OF BIGFLD.
C     JMXBIG   - J-DIMENSION OF BIGFLD.
C     IMXSML   - I-DIMENSION OF THE SUB-GRID  SMLFLD.
C     JMXSML   - J-DIMENSION OF THE SUB-GRID  SMLFLD.
C     ICORNR   - I-VALUE OF LOWER LEFT CORNER OF SUB-GRID
C                IN GRID UNITS OF BIGFLD'S COORDINATE SYSTEM.
C     JCORNR   - J-VALUE OF LOWER LEFT CORNER OF SUB-GRID
C                IN GRID UNITS OF BIGFLD'S COORDINATE SYSTEM.
C     AMULT1   - MULTIPLICATIVE FACTOR USED TO RESCALE DATA.
C     ADD1     - ADDITIVE FACTOR USED TO RESCALE DATA.
C                THIS FIRST PAIR IS USUALLY USED TO CONVERT FROM
C                UNITS OF THE DATA AS THEY WERE PACKED, INTO
C                MORE COMMONLY USED METEOROLOGICAL UNITS.
C     AMULT2   - MULTIPLICATIVE FACTOR USED TO RESCALE DATA.
C     ADD2     - ADDITIVE FACTOR USED TO RESCALE DATA.
C                THIS SECOND PAIR IS USUALLY USED TO PREPARE
C                FOR CONTOURING WITH ONE UNIT CHANGE IN DATA PER
C                DESIRED CONTOUR INTERVAL.
C
C   OUTPUT ARGUMENT LIST:
C     SMLFLD   - REAL*4  SMLFLD(IMXSML,JMXSML)
C                THE 2-DIMENSIONAL ARRAY FOR STORING THE RESULTING
C                SUB-GRID-POINT DATA INTO.
C     IEXIT    - RETURN CONDITIONS.
C                IEXIT = 0  FOR NORMAL EXIT
C                IEXIT = NON-ZERO FOR ERROR RETURN.
C                RETURN CONDITIONS:
C                = 1  IF GIVEN IMXSML OR JMXSML IS .LE. 0
C                = 2  IF THE SUB-GRID LIES ENTIRELY OUTSIDE OF
C                     BIGFLD AREA.
C                = 3  IF EITHER MULTIPLICATIVE FACTOR IS ZERO
C                     WHICH, IF USED, WOULD HAVE ZEROED OUT THE
C                     RESULTING ARRAY.
C                = 4  IF GIVEN IMXBIG OR JMXBIG IS .LE. 0
C
C
C REMARKS: IF THE RESULTING SMALL GRID EXTENDS BEYOND EDGE OF
C     BIGFLD,  THOSE OFF-GRID POINTS WILL BE FILLED WITH HEX 7FFFFFFF.
C     THE TWO ARRAYS -- BIGFLD & SMLFLD -- CANNOT BE EQUIVALENCED.
C
C ATTRIBUTES:
C   LANGUAGE: F90
C   MACHINE:  IBM
C
C$$$
C
C     *     *     *     *     *     *     *     *     *     *     *
C
      INTEGER    IMXBIG, JMXBIG, IMXSML,JMXSML
C
      REAL       BIGFLD(IMXBIG,JMXBIG)
      REAL       SMLFLD(IMXSML,JMXSML)
C
      IEXIT = 0
      IF(JMXSML .LE. 0) GO TO 900
      IF(IMXSML .LE. 0) GO TO 900
C
      IF(IMXBIG .LE. 0) GO TO 933
      IF(JMXBIG .LE. 0) GO TO 933
C
      IF(AMULT1 .EQ. 0.0) GO TO 922
      IF(AMULT2 .EQ. 0.0) GO TO 922
C
      JSTRTS = 0
      ISTRTS = 0
      J1 = JCORNR
      J2 = J1 + JMXSML - 1
C     ... WHAT IF J1 IS BELOW BOTTOM OF BIGFLD?
      IF(J1 .GT. 0) GO TO 233
C     ... ELSE, FIRST ROW OF SUBSET GRID LIES SOMEWHERE
C     ...   BELOW BOTTOM OF BIGFLD
      JSTRTS = IABS(J1) + 1
      J1 = 1
  233 CONTINUE
      IF(J1 .GT. JMXBIG) GO TO 911
      IF(J2 .GT. JMXBIG) J2 = JMXBIG
      IF(J1 .GT. J2) GO TO 911
C
      I1 = ICORNR
      I2 = I1 + IMXSML - 1
      IF(I1 .GT. 0) GO TO 236
C     ... ELSE, FIRST COLUMN OF SUBSET GRID LIES SOMEWHERE
C     ...   OUTSIDE OF LEFT EDGE OF BIGFLD.
      ISTRTS = IABS(I1) + 1
      I1 = 1
  236 CONTINUE
      IF(I1 .GT. IMXBIG) GO TO 911
      IF(I2 .GT. IMXBIG) I2 = IMXBIG
      IF(I1 .GT. I2) GO TO 911
C
      JS = JSTRTS
      DO  366  JB = J1,J2
          JS = JS + 1
          IS = ISTRTS
          DO  355  IB = I1,I2
             IS = IS + 1
             SMLFLD(IS,JS)=(BIGFLD(IB,JB)*AMULT1 + ADD1) * AMULT2 + ADD2
  355     CONTINUE
  366 CONTINUE
      GO TO 999
C
  900 CONTINUE
C     ... COMES HERE IF GIVEN IMXSML OR JMXSML IS .LE. 0
      IEXIT = 1
      PRINT *,'**ABEND IN SUBR SUBFLD.  CONDITION=',IEXIT
      GO TO 999
C
  911 CONTINUE
C     ... COMES HERE IF ICORNR OR JCORNR WAS GIVEN SUCH THAT
C     ...   THE SUBGRID  IS POSITIONED ENTIRELY OUTSIDE OF BIGFLD.
      IEXIT = 2
      PRINT *,'**ABEND IN SUBR SUBFLD.  CONDITION=',IEXIT
      GO TO 999
C
  922 CONTINUE
C     ... COMES HERE IF EITHER MULTIPLICATIVE FACTOR IS 0.0
C     ... WHICH WOULD RESULT IN A ZERO ARRAY
      IEXIT = 3
      PRINT *,'**ABEND IN SUBR SUBFLD.  CONDITION=',IEXIT
      GO TO 999
C
  933 CONTINUE
C     ... COMES HERE IF IMXBIG OR JMXBIG WAS .LE. 0
      IEXIT = 4
      PRINT *,'**ABEND IN SUBR SUBFLD.  CONDITION=',IEXIT
      GO TO 999
C
  999 CONTINUE
      RETURN
      END
