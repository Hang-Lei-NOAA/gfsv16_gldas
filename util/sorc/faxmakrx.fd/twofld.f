      SUBROUTINE TWOFLD(FLDIN1,FLDIN2,FLDIN3,IMAX,JMAX,KEYIDX,IRTN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TWOFLD      COMPUTE TWO INPUT GRIDPOINT DATA FIELDS
C   PRGMMR: KRISHNA KUMAR      ORG: W/NP12   DATE: 1999-08-01
C
C ABSTRACT: COMPUTE TWO INPUT GRIDPOINT DATA FIELDS INTO ONE OUTPUT
C   FIELD.
C
C PROGRAM HISTORY LOG:
C   89-06-12  LUKE LIN
C   89-09-01  GLORIA DENT    ADD KEYIDX PARAMETER TO SUBTRACT PRECIPI-
C                            TATION FIELDS
C   89-11-14  LUKE LIN       ADD KEYIDX PARAMETER FOR ISOTACHS
C   95-02-14  HENRICHSEN     COMPLETE REWRITE AND ADDED LOGIC TO FORM
C                            VORTICITY FIELD FROM U & V FIELDS,
C                            ADD AN ERROR RETURN ARG (IRTN),
C                            ADD COMMON/POLE/ , ADD INLINE DOCUMENTATION.
C   95-02-15  LUKE LIN       CONVERT IT TO CFT-77
C   95-06-17  LUKE LIN       MODIFY FOR PRECIP WITH DIFFERENT CAL. FORMULA.
C   97-02-18  LUKE LIN       ADD KEYIDX 34 FOR HEIGHT CHANGE.
C 1999-08-01  KRISHNA KUMAR  ADD POLENEW COMMON BLOCK - MODIFY THE CODE TO
C                            RUN ON IBM RS/6000 SP SYSTEM
C
C USAGE:    CALL TWOFLD(FLDIN1,FLDIN2,FLDIN3,IMAX,JMAX,KEYIDX,IRTN))
C   INPUT ARGUMENT LIST:
C     FLDIN1   - FIRST INPUT GRID DATA
C     FLDIN2   - SECOND INPUT GRID DATA
C     IMAX     - I-DIMENSIONS OF GRIDS
C     JMAX     - J-DIMENSIONS OF GRIDS
C     KEYIDX   - KEY INDEX OF DATA TYPE (SEE BLOCK DATA)
C
C   OUTPUT ARGUMENT LIST:
C     FLDIN1   - OUTPUT ARRAY FLDIN1(IMAX,JMAX) CONTAINS RESULT.
C     FLDIN3   - SAVE FOR FLDIN1 IF ISOTACHS
C     IRTN     - RETURN CONDITION.
C              - = 0 GOOD RETURN
C              - NE 0 ERROR RETURN FROM SUB ABSVOR DO NOT MAKE MAP.
C
C ATTRIBUTES:
C   LANGUAGE: F90
C   MACHINE:  IBM
C
C$$$
C
      COMMON  /POLE/ XPOL,YPOL,GDTYPE
ckumar
      COMMON  /POLENEW/ XMESHL
ckumar
      COMMON /GRB2/ ALUGRB,ALUGRBIX,AFCSTHR,AKEYIDX,CALFLAG,GRB2FLAG
      INTEGER       ALUGRB
      INTEGER       ALUGRBIX
      INTEGER       AFCSTHR
      INTEGER       AKEYIDX
      INTEGER       CALFLAG
      LOGICAL       GRB2FLAG
C
C     ...THE POLE POSITION IN GRID(65,65) IS AT GRID(33,33).
C     ... GRID MESH LENGHT = XMESHL = 381.0 KLM
C     ...THE POLE POSITION IN GRID(53,45) IS AT GRID(27,49).
C     ... GRID MESH LENGHT = XMESHL = 190.5 KLM
C     ...THE POLE POSITION IN GRID(53,57) IS AT GRID(27,49).
C     ... GRID MESH LENGHT = XMESHL = 190.5 KLM
C
      REAL        FLDIN1(IMAX,JMAX)
      REAL        FLDIN2(IMAX,JMAX)
      REAL        FLDIN3(IMAX,JMAX)
      REAL        WORK(65,6)
      REAL        TINY
C
C??   DATA TINY / 5.0E-7 /
      DATA TINY / 5.0E-4 /
      IRTN = 0
C
       IF (KEYIDX.EQ.10 .OR. KEYIDX.EQ.40) THEN
C
C       CACULATE WIND SPEED FOR ISOTACH FILED.
C        WHERE FLDIN2 CONTAINS THE U'S
C        AND FLDIN1 CONTIANS THE V'S.
C
            DO  J= 1,JMAX
               DO  I =1,IMAX
                     GU = FLDIN2(I,J)
                     GV = FLDIN1(I,J)
                     FLDIN3(I,J) = FLDIN1(I,J)
                     SPEED = SQRT(GU*GU+GV*GV)
                     IF(SPEED.LE.0.0) SPEED = 0.0
                     FLDIN1(I,J) = SPEED
               ENDDO
            ENDDO
      ELSE IF (KEYIDX.EQ.3 .OR. KEYIDX.EQ.33) THEN
C
C       CACULATE THICKNESS FROM TWO HEIGHT FIELDS.
C       WHERE FLDIN1 HAS THE HEIGHER HEIGHT SUCH AS 500MB
C       AND FLDIN2 CONTAINS THE LOWER HEIGHT SUCH AS 1000MB
C
            DO  J = 1,JMAX
               DO  I = 1,IMAX
                   FLDIN1(I,J) = FLDIN1(I,J)-FLDIN2(I,J)
               ENDDO
            ENDDO
      ELSE IF (KEYIDX.EQ.18) THEN
C
C       CACULATE BLEND AVERAGE
C
            DO  J = 1,JMAX
               DO  I = 1,IMAX
                   FLDIN1(I,J) = (FLDIN1(I,J)+FLDIN2(I,J))/2.0
               ENDDO
            ENDDO
      ELSE IF (KEYIDX.EQ.34 .OR.KEYIDX.EQ.23) THEN
C
C       CACULATE HEIGHT CHANGE
C
            DO  J = 1,JMAX
               DO  I = 1,IMAX
                   FLDIN1(I,J) = FLDIN1(I,J)-FLDIN2(I,J)
               ENDDO
            ENDDO
      ELSE IF (KEYIDX.EQ.7 .OR. KEYIDX.EQ.30 .OR. KEYIDX.EQ.37
     1         .OR. KEYIDX.EQ.49 .OR. KEYIDX.EQ.19) THEN

C    ...DO SPECIAL PRECIPITATION TEST FOR VALUES LESS THAN .01 INCHES...
C     WHERE FLDIN2 CONTAINS THE 12-HR ACCUM
C     WHERE FLDIN1 IS THE ACCUM FOR FIRST 6-HR
         IF (CALFLAG .LT. 0) THEN
C          ... SUBTRACT 6 HR APCP FROM 12 HOUR FCST AND 6 HOUR FCST
           DO  J=1,JMAX
               DO  I=1,IMAX
                  DIFFER = FLDIN1(I,J) - FLDIN2(I,J)
                  IF (FLDIN1(I,J) .GE. TINY )THEN
                    IF (FLDIN2(I,J) .LT. TINY ) DIFFER = FLDIN1(I,J)
                  ELSE
                    DIFFER = 0.0
                  ENDIF
                  IF ( DIFFER .LT. TINY ) DIFFER = 0.0
                  FLDIN1(I,J) = DIFFER
                ENDDO
             ENDDO
         ELSE
C          .... PLUS 12 APCP FROM 12 FCST AND 24 FCST LIKE PRECIP
           PRINT *,' PLUS TWO PRCIP FIELDS TOGETHER'
           DO  J=1,JMAX
               DO  I=1,IMAX
                  FLDIN1(I,J) = FLDIN2(I,J) + FLDIN1(I,J)
                  IF ( FLDIN1(I,J) .LT. TINY ) FLDIN1(I,J) = 0.0
                ENDDO
           ENDDO
         ENDIF
      ELSE IF (KEYIDX.EQ.9) THEN
C
C        FORMING THE VORTICITY FROM U & V FIELDS
C        WHERE FLDIN2 CONTAINS THE U'S
C        AND FLDIN1 CONTIANS THE V'S.
C
           KSOUTH = 0
           CALL ABSVOR(KSOUTH,FLDIN2,FLDIN1,IMAX,JMAX,XMESHL,XPOL,
     1               YPOL,WORK,FLDIN3,IERROR)
          IF (IERROR .EQ. 0 )THEN
            DO  J = 1,JMAX
               DO  I = 1,IMAX
                   FLDIN1(I,J) = FLDIN3(I,J)
               ENDDO
            ENDDO
           WRITE(6,FMT='('' TWOFLD: SUB ABSVOR MADE A VORTICITY '',
     1        ''FIELD FROM U AND V FIELDS.'')')
           IRTN = 0
          ELSE
           WRITE(6,FMT='('' TWOFLD: ERROR RETURN FROM ABSVOR='',I3,
     1        '' WILL NOT MAKE MAP.'')')IERROR
           IRTN = 1
          ENDIF
      ENDIF
      RETURN
      END
