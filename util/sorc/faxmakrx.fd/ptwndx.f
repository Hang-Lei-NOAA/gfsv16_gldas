      SUBROUTINE PTWNDX(FLD1,FLD2,IMAX,JMAX,I1BIG,J1BIG,IGPLOT,
     1                  ISTART,JSTART,DOTSGI)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK  ***
C
C SUBPROGRAM:  PTWNDX   FORMS WIND PLOTS FROM GRID POINT U/V DATA.
C   AUTHOR: KRISHNA KUMAR         ORG:W/NP12        DATE:  1999-08-01
C
C ABSTRACT:  CONVERTS UNPACKED SCALED GRID POINT U'S AND V'S TO
C   WIND DIRECTION AND SPEED AND PLOTS AS WIND SHAFT WITH BARBS FOR
C   A FAX OR VARIAN CHART.
C
C PROGRAM HISTORY LOG:
C   85-05-06  PETER HENRICHSEN
C   93-04-28  LUKE LIN   CONVERT TO FORTRAN-77 AND ADD DOC BLOCK.
C   97-03-13  LUKE LIN   MODIFY FOR CFT USED.
c   97-12-05  CHRIS CARUSO  ADD COMMON BLOCK MUTCON AND ADD SPECIAL
C                           CODE FOR GEOSTROPHIC WIND HANDLING.
C                           (ADD BITMAP TO SEE WHICH WINDS TO TURN
C                           OFF).
C 1999-08-01  KRISHNA KUMAR CONVERTED THIS CODE FROM CRAY TO IBM
C                           RS/6000. 
C
C USAGE:  CALL PTWNDX(FLD1,FLD2,IMAX,JMAX,I1BIG,J1BIG,
C                     IGPLOT,DOTSGI)
C   INPUT ARGUMENTS:
C     FLD1 = FIELD CONTAINING THE V GRID POINT DATA.
C     FLD2 = FIELD CONTAINING THE U GRID POINT DATA.
C     IMAX,JMAX = THE DIMENIONS OF FLD1 AND FLD2.
C     VRTLON = VERTICAL LON OF GRID.  FOR LFM  VRTLON = 105.0 FOR PE
C               VRTLON =  80.0.
C     DOTSGI = DOTS PER GRID INTERVAL WHICH IS A FUNCTION OF BACKGROUND.
C     IGPLOT = INTEGER*2 TWO WORD ARRAY THAT CONTAINS THE FOLLOWING:
C     IGPLOT(1) CONTAINS THE KEY FOR DETERMINING THE KIEL FOR SUB TRULL.
C     IGPLOT(2) CONTAINS THE PLOTTING INCREMENT OF THE WINDS IE.
C                   IF .LE.1 PLOT AT EVERY GRID POINT,
C                   IF = 2 PLOT AT EVERYOTHER GRID POINT,
C                   IF = 3 PLOT AT EVERY 3RD GRID POINT.
C
C   INPUT FILES:  NONE
C
C
C   OUTPUT ARGUMENTS: NONE
C
C   OUTPUT FILES: NONE
C
C
C
C   RETURN CONDITIONS:NONE.
C
C   SUBPROGRAMS CALLED:
C     UNIQUE :  TRULL   TUWNDR  WNDBRK  WNDIRT
C
C     LIBRARY:  NONE
C
C ATTRIBUTES:
C   LANGUAGE:   FORTRAN 90
C   MACHINE:    IBM
C
C$$$
C
C
      COMMON /MUTCON/ KEYIDX,UA1V,UA2V,UA3V,UM1V,UM2V,UM3V,LINEVU,LINEP,
     X               IGRIDP,T1
C
C
      COMMON/KPLOT/LABEL(2,1024),LABIX,NOBUF,IDRA(50)
      COMMON/PRINT/LPRINT
C
C
C
C
      REAL        FLD1(IMAX,JMAX)
      REAL        FLD2(IMAX,JMAX)
      REAL        GU,GV,ANG,VRTLON,ALAT,ALON,T1
      REAL        XI, XJ
C
      INTEGER     IADD(3)
      INTEGER     ISTR(3)
      INTEGER     JADD(3)
      INTEGER     JSTR(3)
      CHARACTER*12 JTRUDR
      INTEGER     KIEL(3)
C
      DATA        IADD           /4,12,0/
      DATA        ISTR           /3,1,1/
      DATA        JADD           /4,08,0/
      DATA        JSTR           /3,1,1/
      DATA        KIEL           /1,13,2/
C
      INTEGER     IGPLOT(2)
      INTEGER     LANDSEA(53,42)
C
      LOGICAL    LPRINT
C
C
      LPRINT = .FALSE.
      VRTLON = 105.0
C
      T1 = DOTSGI
      KK =  IGPLOT(1)
      IF(KK) 5,5,8
   5  CONTINUE
      KK = 2
      IF(T1.LT.54.) KK = 1
C
   8  CONTINUE
C
C  IF GEOSTROPHIC WIND, READ IN BITMAP.  WILL BE USED TO SEE WHICH
C  GRIDPOINTS WILL NOT HAVE WIND BARB PLOTTED (AT THESE GRID POINTS,
C  GEOSTOPHIC WIND IS UNRELIABLE DUE TO LACK OF INPUT DATA). KEYIDX
C  EQUALS 17 IS SFC GEOSTROPHIC WIND.
C
      IF(KEYIDX.EQ.17) THEN
        DO J = 42,1,-1
          READ(8,200,END=201) (LANDSEA(I,J),I=1,53)
 200      FORMAT(53I1)
c          print 200,(landsea(i,j),i=1,53)
        ENDDO 
      ENDIF       
 201  CONTINUE
C
C
C      LOOP THRU U, AND V FIELDS AND THEN CONVERT TO WIND DIR AND SPEED.
C
C
C            ISTART = ISTR(KK)
C            JSTART = JSTR(KK)
             ICRMNT = 1
             JCRMNT = 1
C?           I1BIG = 4
C?           J1BIG = 4
             IF(I1BIG.EQ.0) I1BIG = 17
             IF(J1BIG.EQ.0) J1BIG = 10
             IGDCOR= I1BIG - IADD(KK)
             JGDCOR= J1BIG - JADD(KK)
             MM = IGPLOT(2)
             IF(IGPLOT(2).LE.1) MM = 1
C
      PRINT  10, KK,KIEL(KK),IGDCOR,JGDCOR,MM
  10  FORMAT('  KK=',I2,' KIEL=',I2,
     $ ' IGDCOR=',I2,' JGDCOR=',I2,' PLOTTING INCREMENT=',I2)
      PRINT  20 ,JSTART,ISTART,JCRMNT,ICRMNT
  20  FORMAT('  JSTART=',I2,' ISTART=',I2,' JCRMNT=',I2,' ICRMNT=',I2)
      PRINT *,' IMAX=', IMAX, ' JMAX=', JMAX
C
             N = 1
             IF(MM.NE.1)N = MM
C     WHICH WILL MAKE SURE THAT THE FIRST GRID POINT IS ALWAYS PLOTTED
C
            DO  340 J= JSTART,JMAX,JCRMNT
C
               DO 330 I =ISTART,IMAX,ICRMNT
C
C              PLOT EVERY OTHER GRID POINT...........................
C
               NODD = MOD(N,MM)
C
                   IF(NODD.NE.0) GO TO 320
C
C              CHECK BITMAP TO SEE IF IT'S A GRIDPOINT WHERE WE DON'T
C              WANT TO PLOT A WIND BARB.  ONLY DO THIS FOR SFC GEO
C              WIND (KEYIDX = 17)...
C
               IF(KEYIDX.EQ.17) THEN
                 IF(LANDSEA(I,J).EQ.2) GO TO 320
               ENDIF
C
C              CORRECT GRID COORIDINATE TO PROPER BACKGROUND FOR CALL
C              TO TRULL.....
                      XI = FLOAT(I + IGDCOR)
                      XJ = FLOAT(J + JGDCOR)
C
C              CONVERT GRID LOCATION TO DOTS FOR CALL TO WNDBRK...
C
C???                   JSTN = T1*(XJ-1.0) + 0.5
C??/                   ISTN = T1*(XI-1.0) + 0.5
                       JSTN = NINT(T1*(XJ-1.0))
                       ISTN = NINT(T1*(XI-1.0))
C
C
                       GU=FLD2(I,J)
                       GV=FLD1(I,J)
C
C                       GU =  U COMP OF WIND AT GRID POINT IN KTS.
C                       GV =  V COMP OF WIND AT GRID POINT IN KTS.
C
C
C
C
C      GET LON OF GRID POINT  TO COMPUTE TRUE WIND DIRECTION
C
                       CALL TRULL(XI,XJ,ALAT,ALON,KIEL(KK))
C
C      GET WIND DIR AND SPEED
C
                       KEY = 0
                         ANG = 0.0
C
                       CALL TUWNDR(GU,GV,IDIR,
     1                 ISPEED,ANG,VRTLON,ALON,ITRUDR,KEY)
C
C      IDIR=GRID ORENTATED DIRECTION OF WIND TO NEAREST 10DEG,IF KEY =0
C      IDIR=GRID ORENTATED DIRECTION OF WIND TO NEAREST 1DEG,IF KEY= 1
C      ISPEED= WIND SPEED TO NEAREST KT.
C      ITRUDR= TRUE WIND DIRECTION TO NEAREST 10 DEGS.
C      VRTLON = VERTICAL LON OF GRID.  FOR LFM  VRTLON = 105.0 FOR PE
C               VRTLON =  80.0.
C      ANG    = ANGLE  OR DIFFERENCE BETWEEN THE VRTLONS' OF DIFFERENT
C               GRIDS. TO ROTATE FROM LFM TO PE ANG = +25.0.
C
C
C
C  *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *   *
C
C
C
                  IF(ISPEED.LE.0) GO TO  320
C
                  IF(ISPEED.GT.300) ISPEED = 300
                  IF(IDIR  .EQ.0.AND.KEY.EQ.0) IDIR   = 36
                  IF(IDIR  .EQ.0.AND.KEY.EQ.1) IDIR   = 360
                  IDDGD = IDIR
C
C
C
C
C
C
C
C . . . . CONVERT TRUE WIND DIRECTION TO HOLLERTH................
C
              CALL BIN2EB(ITRUDR,JTRUDR,2,'A999')
C
                      IF(LPRINT)PRINT  305,ISPEED,IDDGD,GU,GV
  305 FORMAT('  ISPEED= ',I4,' IDDGD=',I4,' GU=',F6.2,' GV=',F6.2)
C
C
                  CALL WNDBRK(ISTN,JSTN,IDDGD,ISPEED,NWOK,0,IRR)
                  CALL WNDIRT(ISTN,JSTN,JTRUDR,IDDGD,ISPEED)
C
  320             CONTINUE
                    N = N + 1
C
C
  330          CONTINUE
  340       CONTINUE
      RETURN
      END
