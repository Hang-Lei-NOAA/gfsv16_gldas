      SUBROUTINE THINLB
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    THINLB      THIN PUTLAB LABELS
C   PRGMMR: KRISHNA KUMAR         ORG: W/NP12   DATE: 1999-08-01
C
C ABSTRACT: SEARCHES THROUGH LABSET SETS OF LABELS ONE AT A TIME
C           (AS WRITTEN BY SAVLAB),THINS OVERLAPPING LABELS WITH
C           A VALUE COMPARISON TEST, AND CALLS DGNTXT FOR THE
C           SURVIVING LABELS.
C
C PROGRAM HISTORY LOG:
C   85-08-05  KEVIN HLYWIAK
C   88-04-19  GLORIA DENT PUT IN DOCUMENTATION BLOCK.
C   96-03-15  LUKE LIN    CONVERT TO CFT-77, ONLY FOR AFOS.
C 1999-08-01  KRISHNA KUMAR CONVERTED THIS CODE FROM CRAY TO IBM RS/6000.
C
C USAGE:    CALL THINLB
C   INPUT ARGUMENT LIST:
C     IWID     - INTEGER*4 IWID THE DOT WIDTH OF THE CHARACTER SET
C     IHGT     - INTEGER*4 IHGT THE DOT HIEGHT OF THE CHARACTER SET
C     ISEP     - OVERLAP CRITERIA IN DOT UNITS (COMPARISON TEST)
C     IPLT     - IPLT=0 NO THINNING OF THIS SET OF LABELS
C              - IPLT=NONZERO THINNING OF THIS SET OF LABELS IS DONE
C
C   OUTPUT FILES:
C     FT06F001 - PRINT MESSAGES OF PROGRESS OF EACH ENTRY
C
C REMARKS:
C
C ATTRIBUTES:
C   LANGUAGE: F90
C   MACHINE:  IBM
C
C$$$
C
      COMMON/PUTARG/PUTHGT,PUTANG,IPRPUT(2),ITAPUT
C
      COMMON/LABARG/IPOS(500),JPOS(500),TEXT1(500),
     X   VAL(500),NCH(500),NSAVED
      CHARACTER*4 TEXT1
      INTEGER   IWID,IHGT,ISEP
      CHARACTER*4 ITEXTC
      INTEGER     IDEL, JDEL
C     
C     INTEGER IDOT(500)
C     INTEGER JDOT(500)
C
      DATA IWID    /12/
      DATA IHGT    /14/
      DATA ISEP    /25/
      DATA IPLT    /1/
C
      DATA SCALE   /18.0/
      DATA IDEL    /-25/
      DATA JDEL    /-7/
C
      PRINT 10,IWID,ISEP,IPLT,NSAVED
   10 FORMAT(' ENTERING SUBROUTINE THINLB DATED 9/12/96',/,' IWID=',I4,
     X '  ISEP=',I4,'  IPLT=',I2,
     X '  NSAVED=',I4)
C
C     DO 20 I=1, NSAVED
C        IDOT(I) = XPOS(I)*SCALE + IDEL
C        JDOT(I) = YPOS(I)*SCALE + JDEL
C 20  CONTINUE
C
      NGO=0
      NLABS=NSAVED
      L1MAX=NLABS-1
      DO 60 L1=1,L1MAX
         IF(IPOS(L1).EQ.9999) GOTO 60
         L2MIN=L1+1
         DO 50 L2=L2MIN,NLABS
            IF(IPOS(L2).EQ.9999) GOTO 50
            JB1=JPOS(L1)-ISEP/2
            JT2=JPOS(L2)+IHGT+ISEP/2
            IF(JB1.GE.JT2) GOTO 50
            JT1=JPOS(L1)+IHGT+ISEP/2
            JB2=JPOS(L2)-ISEP/2
            IF(JT1.LE.JB2) GOTO 50
            IL1=IPOS(L1)-ISEP/2
            IR2=IPOS(L2)+NCH(L2)*IWID+ISEP/2
            IF(IL1.GE.IR2) GOTO 50
            IR1=IPOS(L1)+NCH(L1)*IWID+ISEP/2
            IL2=IPOS(L2)-ISEP/2
            IF(IR1.LE.IL2) GOTO 50
C   FALL THRU MEANS L1 AND L2 OVERLAP, SO STRIKE ONE BY TESTING VALUE
            IF(ABS(VAL(L1)).GE.ABS(VAL(L2))) IPOS(L2)=9999
            IF(ABS(VAL(L1)).LT.ABS(VAL(L2))) IPOS(L1)=9999
            IF(IPOS(L2).EQ.9999.OR.IPOS(L1).EQ.9999)NGO=NGO+1
   50    CONTINUE
   60 CONTINUE
C
      NSTAY=NLABS-NGO
      PRINT 70,NSTAY,NLABS
   70 FORMAT('  ',I4,' OF',I4, 'LABELS SURVIVED.')
      DO 89 IPUT=1,NLABS
         IF(IPOS(IPUT).EQ.9999) GOTO 89
         ITEXTC=TEXT1(IPUT)
         CALL PUTLAB(IPOS(IPUT),JPOS(IPUT),PUTHGT,ITEXTC,PUTANG,4,
     1               IPRPUT,ITAPUT)
   89 CONTINUE
      RETURN
      END
C
C
