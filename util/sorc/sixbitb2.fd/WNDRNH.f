      SUBROUTINE WNDRNH(ULOLA,VLOLA,ALAT,WLONG,ORIENT,
     1                  ISPEED,IDDTRU,IDDGRD)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: WNDRNH         WIND DIRECTION/SPEED FROM U & V (LOLA)
C   PRGMMR: LILLY            ORG: NMC/412    DATE: 95-05-05
C
C ABSTRACT: USING THE GIVEN U- AND V-COMPONENTS OF THE WIND (EARTH-
C   ORIENTED),  THIS ROUTINE CALCULATES  (A) THE WIND SPEED,
C   (B) THE EARTH-ORIENTED WIND DIRECTION,  AND (C) THE NORTHERN-
C   HEMISPHERIC POLAR-STEREOGRAPHIC GRID-ORIENTED WIND DIRECTION.
C
C PROGRAM HISTORY LOG:
C   82-04-22  SHIMOMURA
C   95-05-05  LILLY  CONVRT SUBROUTINE TO FORTRAN 77
C
C USAGE:  CALL WNDRNH(ULOLA, VLOLA, ALAT, WLONG, ORIENT,
C                  ISPEED, IDDTRU, IDDGRD)
C
C   INPUT ARGUMENTS:
C     (1)ULOLA ... WEST-TO-EAST COMPONENT OF WIND
C                  AS FOUND, FOR EXAMPLE, IN THE U-COMPONENT DATA
C                  IN LONGITUDE/LATITUDE (LOLA) GRID.
C     (2)VLOLA ... SOUTH-TO-NORTH COMPONENT OF WIND
C                  AS FOUND, FOR EXAMPLE, IN THE V-COMPONENT DATA
C                  IN LONGITUDE/LATITUDE (LOLA) GRID.
C     (3)ALAT  ... LOCATION OF THIS WIND DATA ON EARTH
C                  IN DEGREES NORTH LATITUDE
C     (4)WLONG ... AND DEGREES WEST LONGITUDE.
C     (5)ORIENT... ORIENTATION OF DESTINATION GRID FOR CALCULATING
C                  THE POLAR-STEREO GRID-ORIENTED WIND DIRECTION.
C                  ORIENT IS THE VERTICAL MERIDIAN OF POLAR
C                  STEREOGRAPHIC NORTHERN HEMISPHERIC GRID.
C                  ORIENT = 80.0 FOR NMC STD 65*65 GRID
C                  ORIENT =105.0 FOR NMC LFM GRID.
C
C   OUTPUT ARGUMENTS:
C     (6)ISPEED... WIND SPEED (I*4) IN SAME UNITS AS GIVEN COMPONENTS
C     (7)IDDTRU... WIND DIRECTION (I*4), EARTH-ORIENTED,
C                  IN TENS OF DEGREES.
C     (8)IDDGRD... WIND DIRECTION (I*4), N.HEMI. GRID ORIENTED,
C                  IN TENS OF DEGREES.
C
C   REMARKS: WIND DIRECTION = 36 FOR NORTH WIND
C     WIND DIRECTION = 00 FOR CALM WIND.
C     IDDTRU AT NORTH POLE RETURNS WITH THE WIND DIRECTION  AS THE
C     MERIDIAN (WEST LONGITUDE) FROM WHICH THE WIND IS BLOWING
C     TOWARD THE POLE.
C
C ATTRIBUTES:
C   LANGUAGE: CF77     
C   MACHINE : CRAY4                                     
C
C$$$
C
      DATA     CNV2RA / 0.0174533 /
      DATA     CNV2DE / 57.29578 /
C
      UL = ULOLA
      VL = VLOLA
      SPEED = SQRT(UL*UL + VL*VL)
      ISPEED = SPEED + SIGN(0.5,SPEED)
      IF(ISPEED .GT. 0) GO TO 300
C     ... ELSE, CALM WIND HERE ...
      IDDTRU = 0
      IDDGRD = 0
      GO TO 999
C
C     ... COMES HERE IF NOT CALM WIND
  300 CONTINUE
      DIR = 270.0 - CNV2DE*ATAN2(VL,UL)
C     ... WHERE DIR IS DIRECTION FROM WHICH WIND IS BLOWING
C     ...    IN USUAL EARTH-ORIENTED DIRECTION
      IDDTRU = 0.1*DIR + SIGN(0.5,DIR)
      IF(ALAT .LE. 89.0) GO TO 333
C     ... ELSE, SPECIAL HANDLING FOR POLE POINT
C     ... ROTATE COORDINATE AXIS SO THE VECTOR IS FOR OBSERVER AT 180
      THETAR = (WLONG-180.0) * CNV2RA
      ALFAR = ATAN2(VL,UL)
      PHIR = ALFAR - THETAR
      UPRIME = SPEED * COS(PHIR)
      VPRIME = SPEED * SIN(PHIR)
C     ... WHERE UPRIME,VPRIME IS FOR ROTATED TO 180.0 DEGREESMERIDIAN
      DIRNP = 270.0 - CNV2DE*ATAN2(VPRIME,UPRIME)
      IDDTRU = 0.1*DIRNP + SIGN(0.5,DIRNP)
      GO TO 333
C
  333 CONTINUE
      IF(IDDTRU .GT. 36) IDDTRU = IDDTRU - 36
      IF(IDDTRU .LE. 0) IDDTRU=36+IDDTRU
C     ... NORTH WIND WILL HAVE 36 ... NOT 00
C
C     ... NEXT, TO GET NMC PS STD NMC GRID ORIENTED DIRECTION ...
      DDDGRD = DIR + (WLONG - ORIENT)
      IDDGRD = 0.1*DDDGRD + SIGN(0.5,DDDGRD)
      IF(IDDGRD .GT. 36) IDDGRD = IDDGRD - 36
      IF(IDDGRD .LE. 0) IDDGRD = 36+IDDGRD
      GO TO 999
C
  999 CONTINUE
      RETURN
      END
