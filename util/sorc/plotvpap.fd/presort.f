      SUBROUTINE PRESORT(KRUN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    PRESORT     PRE THINNER PROCESSOR
C   PRGMMR: SHIMOMURA        ORG: W/NP12     DATE: 97-01-14
C
C ABSTRACT: ASSIGNS A NEW OBS_TYPE AND A PRIORITY FOR
C   FOR THINNING (SEE REMARKS FOR FURTHER DETAILS).
C
C PROGRAM HISTORY LOG:
C   YY-MM-DD  ORIGINAL AUTHOR - SHIMOMURA
C   88-07-25  GLORIA DENT  PUT IN DOCUMENTATION BLOCK
C   89-04-17  STEVE LILLY  UPDATE DOCUMENTATION BLOCK
C   93-05-06  LILLY CONVERT SUBROUTINE TO FORTRAN 77
C   97-01-14  SHIMOMURA  - CONVERT FROM IBM FORTRAN TO CRAY FORTRAN
C                        - ADDED COMMENTS
C
C USAGE:    CALL PRESORT  (KRUN)
C
C   INPUT ARGUMENT LIST:
C     KRUN     - THE RUN OPTION READ FROM DATA CARDS IN THE JCL
C     NDATA(1,X)(IBIT 53:42) - GIVEN 12-BIT OBS TYPE
C     NDATA(2,X)(IBIT  3:0)  - GIVEN  4-BIT OFFTIME CODE
C     NDATA(2,X)(IBIT 63:16) - GIVEN  6-CHARACTER STATION NAME
C        WHERE NDATA ARRAY IS FOUND IN COMMON /JSPACE/ ...

C   OUTPUT ARGUMENT LIST:
C     NDATA(1,X)(IBIT 47:42) - REPLACEMENT 6-BIT OBS TYPE
C     NDATA(1,X)(IBIT  4:0)  - PRIORITY FOR THINNING
C
C     COMMON   - /JSPACE/ IDREC(6), NDATA(3,4001)
C
C   OUTPUT FILES:
C     FT06F001 - INCLUDE IF ANY PRINTOUT
C
C REMARKS: THE OBSERVATION-TYPE, GIVEN IN 12 BITS PACKED 
C   IN BITS 11 THRU 22 (COUNTING FROM 1 AT HI-ORDER BIT POSITION) 
C   OF LONGWORD NDATA(1,X), IS REPLACED BY A NEW TYPE CONSISTING  
C   OF 6 BITS AND PACKED INTO  BITS 17 THRU 22 OF THE SAME WORD.  
C   THE PRIORITY (WHICH IS DETERMINED FROM THE GIVEN TYPE AND THE 
C   GIVEN OFF-TIME CODE) IS PACKED RIGHT-JUSTIFIED 5 BITS  IN 
C   LONGWORD NDATA(1,X).
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77
C   MACHINE:  CRAY
C
C$$$
C
      INTEGER    MAXOBS
      PARAMETER (MAXOBS=20000)
      INTEGER    KSIZDATA
      PARAMETER (KSIZDATA=MAXOBS+1)
C     ...IF DIMENSION OF NDATA IS CHANGES, SO MUST MAXOBS BE CHANGED...

      COMMON  /JSPACE/ IDREC(6), NDATA(3,KSIZDATA)
C
      integer*8  NDATA
C


      INTEGER    ITTEST(21)
      DATA       ITTEST       / 11, 12, 13, 21, 22,
     1                          23, 31, 41, 42, 45,
     2                          51, 61, 62, 63,511,
     3                         512,513,521,522,523,551/
C
      INTEGER   KTRA(21)
      DATA      KTRA          / 1, 1, 1, 2, 3,
     1                          3, 5, 8, 8, 7,
     2                          4, 6,12,13, 1,
     3                          1, 1, 2, 3, 3, 4/
C
      INTEGER   NEWTYP(21)
      DATA      NEWTYP        / 1, 2, 3, 4, 5, 
     1                          6, 7, 8, 8, 9,
     2                         10,11,12,13,Z'3E',
     3                       Z'3D',Z'3C',Z'3B',Z'3A',Z'39',Z'36'/
C
C
C     ...TABLE OF TYPES SEQUENTIALLY NUMBERED
C     ...SFC TYPES ARE ONES COMPLEMENTS, DESIGNED TO FIT IN 6 BITS...

C     ...FOLLOWING ARE THE PRIORITIES...

      DATA     KLAND	/ 1/
      DATA     KSHIP 	/ 1/
      DATA     KPIBAL	/ 2/
      DATA     KRECCO	/ 3/
      DATA     KACARN	/ 4/
      DATA     KACON 	/ 5/
      DATA     KACARF	/ 6/
      DATA     KACOFF	/ 7/
      DATA     KSIR  	/ 8/
      DATA     KBOGUS	/ 9/
      DATA     KBLANK	/10/
      DATA     LBOG  	/11/
C
      DATA     mskofftim	/Z'0000000F'/

      DATA     MSKTYP		/Z'00000FFF'/
C     ...WHERE MSKTYP ALLOWS 12-BIT ZONE FOR THE GIVEN OBS TYPE
C     ...   AFTER SHIFTING TO RIGHT-JUSTIFIED
C     ...   WHICH IS REPLACED HEREIN BY A 6-BIT OBS TYPE

       INTEGER     MSKPRI
       DATA        MSKPRI    / X'000000000000001F' /
       INTEGER     NOTPRI
       DATA        NOTPRI    / X'FFFFFFFFFFFFFFE0' /
       INTEGER     NOTTYP
       DATA        NOTTYP    / X'FFC003FFFFFFFFFF' /
C
      integer*8    LONGWORD

      integer*8    LONGWRDB
      CHARACTER*8 CLONGWRDB
      EQUIVALENCE (LONGWRDB,CLONGWRDB)

      INTEGER      IPRIVAL


C     ... FOR LFM OPTION (KRUN = 5) BOOST THE PRIORITY OF TIROS DATA.
C
      IF(KRUN .EQ. 5) KSIR = KPIBAL

      DO  1000  J = 1,MAXOBS
        IF(NDATA(1,J) .EQ. 0) GO TO 1100	!... END ALL OBS

        LONGWORD = NDATA(1,J)
        LONGWRDB = NDATA(2,J)

        ITYPE = ISHFT(LONGWORD,-42)
        ITYPE = IAND(ITYPE,MSKTYP)
C       ... WHERE ITYPE CONTAINS THE OLD, GIVEN, TYPE ...

        DO  72  II = 1,21
          IISV = II
          IF(ITYPE .EQ. ITTEST(II)) GO TO 74
   72   CONTINUE
C       ... IF IT FALLS THRU, THEN NO MATCH FOUND FOR THIS TYPE ...
        WRITE(6, 73) ITYPE,J,LONGWORD,CLONGWRDB(1:6)
   73   FORMAT(1H ,'presort:UNKNOWN TYPE OF OBS = HEX ', Z8,
     1         3X, 'IN NDATA(1,', I5, ') = HEX ',Z16,
     2         3X, 'FOR STATION = ', A6)

C       ...MARK THIS UNKNOWN AS NEWTYP BOGUS W/ ZERO PRIORITY
        LONGWORD = IAND(LONGWORD,NOTTYP)	!... ERASE TYPE ZONE
        LTYPE = LBOG
        LTYPE = ISHFT(LTYPE,42)
        LONGWORD = IOR(LONGWORD,LTYPE)

        NDATA(1,J) = IAND(LONGWORD,NOTPRI)	!... PRIORITY=0
        GO TO 1000		!... JUMP TO ENDDO TO DO NEXT STN

C       . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
   74   CONTINUE
C       ... COMES HERE OUT OF INNER DO 72 WITH MATCH TYPE FOUND AT IISV

        KDSTYP = NEWTYP(IISV)
        KDSTYP = ISHFT(KDSTYP,42)
        LONGWORD = IAND(LONGWORD,NOTTYP)	!... ERASE TYPE ZONE
        LONGWORD = IOR(KDSTYP,LONGWORD)
C       ...WHICH SUBSTITUTED THE SHORTER NEW TYPE IN THE LONGWORD ...
C       ...NEXT... GET PRIORITY... = F(TYPE,OFFTIME)

        KTYPE = KTRA(IISV)
C       ...WHERE KTYPE IS INDEX FOR LONG GO-TO ...
        KOFFTM = IAND(NDATA(2,J),mskofftim)

        IPRIVAL = 0

C ...  GO TO (101,102,102,104,105,106,107,108,120,120,120,112,104),KTYPE
C             1   2   3   4   5   6   7   8   9   10  11  12  13
C ...        GO TO 120

        IF (KTYPE .EQ. 1) THEN
           IF(KOFFTM .LE. 4) THEN
              IPRIVAL = KBLANK
           ELSE
              IPRIVAL = KLAND
           ENDIF

        ELSE IF((KTYPE .EQ. 2) .OR. (KTYPE .EQ. 3)) THEN
           IPRIVAL = KSHIP

        ELSE IF((KTYPE .EQ. 4) .OR. (KTYPE .EQ. 13)) THEN
           IPRIVAL = KBOGUS

        ELSE IF (KTYPE .EQ. 5) THEN
           IPRIVAL = KRECCO

        ELSE IF (KTYPE .EQ. 6) THEN
           IPRIVAL = KSIR

        ELSE IF (KTYPE .EQ. 7) THEN
           IF(KOFFTM .LE. 4) THEN
              IPRIVAL = KACARF
           ELSE
              IPRIVAL = KACARN
           ENDIF

        ELSE IF (KTYPE .EQ. 8) THEN
           IF(KOFFTM .LE. 4) THEN
              IPRIVAL = KACOFF
           ELSE
              IPRIVAL = KACON
           ENDIF

        ELSE IF((KTYPE .EQ. 9)  .OR.
     1          (KTYPE .EQ. 10) .OR.
     2          (KTYPE .EQ. 11)) THEN
           IPRIVAL = KBLANK

        ELSE IF (KTYPE .EQ. 12) THEN
           IPRIVAL = KPIBAL
C
        ELSE
           IPRIVAL = KBLANK
        ENDIF

        GO TO 200

C
  200   CONTINUE
C
        IPRIVAL  = IAND(IPRIVAL,MSKPRI)		!... 4-BIT MSK FOR PRIOR
        LONGWORD = IAND(LONGWORD,NOTPRI)
        LONGWORD = IOR(LONGWORD,IPRIVAL)
        NDATA(1,J) = LONGWORD
C
 1000 CONTINUE
C
C     ... WHICH IS END OF DO LOOP ON EACH OBSERVATION.
C
 1100 CONTINUE
      RETURN
      END
