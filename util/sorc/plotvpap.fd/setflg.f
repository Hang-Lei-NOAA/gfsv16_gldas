      SUBROUTINE SETFLG(NUMFIL,KRUN,FLAGS)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SETFLG      SET PLOTTING OPTIONS AND FILES TO USE.
C   PRGMMR: LILLY            ORG: W/NMC412   DATE: 93-05-04
C
C ABSTRACT: SETS A NUMBER OF SWITCHES FOR MANY COMBINATIONS OF
C   PLOTTING OPTIONS, BASED UPON NUMFIL,FLAGS AND KRUN
C
C PROGRAM HISTORY LOG:
C   89-04-24  ORIGIONAL AUTHOR HENRICHSEN
C   93-05-04  LILLY CONVERT SUBROUTINE TO FORTRAN 77
C
C USAGE:    CALL SETFLG(NUMFIL,KRUN,FLAGS)
C   INPUT ARGUMENT LIST:
C     NUMFIL   - INTEGER   COMPOSIT NUMBER OF FILES TO GET DATA FROM.
C              - SEE REMARKS BELOW.
C     KRUN     - IS THE RUN OPTION READ IN FROM DATA CARDS.
C     FLAGS    - LOGICAL   9 BYTE ARRAY THAT HAVE BEEN SET TO
C              - .TRUE. OR .FALSE. BY SUB CKPARM.
C              - WHERE THE FLAGS ARE:
C              - FLAGS(1) = ACAR
C              - FLAGS(2) = MIDC
C              - FLAGS(2) = IS1
C              - FLAGS(3) = IS2
C              - FLAGS(5) = AFOS
C              - FLAGS(6) = SEND
C              - FLAGS(7) = CARD
C              - FLAGS(8) = MARG
C              - FLAGS(9) = PNCH
C
C   OUTPUT ARGUMENT LIST:
C     COMMON   - /SWT   / IS1,IS2
C     COMMON   - /LACAR / LACAR
C     COMMON   - /MIDCYC/ MIDCYC
C     COMMON  /IAFOS /NUMAFS,NAMPIL(10),LVERSN,AFOS,SEND,CARD,MARG,PNCH
C     COMMON   - /LFILES/ L14,L15,L18,L19,L20,L22
C
C   OUTPUT FILES:
C     FT06F001 - STANDARD PRINT FILE
C
C REMARKS: NUMFIL DEFINES A TWO-DIGET NUMBER USED TO OVERRIDE THE
C     HISTORICAL CONVENTIONS REGARDING WHICH FILES ARE PLOTTED UNDER
C     ANY KRUN OPTION.  IF NUMFIL IS 00 OR MISSING, THEN THEN
C     ALONE WILL DETERMINE WHICH FILES ARE TO BE PLOTTED.
C     OTHERWISE THE NUMBER HERE CAN BE INTERPRETED THUSLY --
C             1    PLOT FT19 (ADPUPA)
C             2    PLOT FT14 (AIRCFT)
C             4    PLOT FT22 (SATWND)
C             8    PLOT FT15 (TIROS)
C            16    PLOT FT18 (BOGUS)
C            32    PLOT FT20 (ACAR)
C     THESE VALUES ARE ADDITIVE.  FOR EXAMPLE, 13 MEANS PLOT FT19,
C     FT22, AND FT15.
C     CURRENTLY THE FOLLOWING FLAGS ARE DEFINED:
C          FLAGS(1)  SIGNALS THE ACAR OPTION
C          FLAGS(2)  ACTIVATES THE MID-CYCLE OPTION
C          FLAGS(3) AND FLAGS(4) TELLS RDSOLD TO BYPASS DATA/TIME
C                     BYPASS DATE/TIME CHECKING
C          FLAGS(5)  SIGNALS TO MAKE AFOS PLOTFILE MAPS.
C          FLAGS(6)  SIGNALS SEND THE PLOTFILE TO AFOS.
C          FLAGS(7)  SIGNALS READ CARDS TO GET FLAGS FOR SUB W3FQ06
C          FLAGS(8)  SIGNALS THAT SOME STATIONS WILL BE PLOTTED IN
C                    MARGIN TO AVOID OVER PLOTTING.
C          FLAGS(9)  SIGNALS THAT AFOS PLOTFILE WILL BE WRITTEN TO
C                    FT28 IN CARD FORMAT 80 BYTE RECORDS.
C
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77
C   MACHINE:  NAS
C
C$$$
C
      COMMON /SWT   / IS1,IS2
      COMMON /LACAR / LACAR
      COMMON /MIDCYC/ MIDCYC
      COMMON /IAFOS /NUMAFS,NAMPIL(10),LVERSN,AFOS,SEND,CARD,MARG,PNCH
      COMMON /LFILES/ L14,L15,L18,L19,L20,L22
C
      INTEGER    LFLDB
      INTEGER    MASK1
      INTEGER    MASK2
      INTEGER    MASK4
      INTEGER    MASK8
      INTEGER    MASK16
      INTEGER    MASK32
C
      DATA       MASK1       /Z'01'/
      DATA       MASK2       /Z'02'/
      DATA       MASK4       /Z'04'/
      DATA       MASK8       /Z'08'/
      DATA       MASK16      /Z'10'/
      DATA       MASK32      /Z'20'/
C
      LOGICAL    AFOS
      LOGICAL    CARD
      LOGICAL    FLAGS(9)
      LOGICAL    LACAR
      CHARACTER*1  LVERSN(40)
      LOGICAL    L14
      LOGICAL    L15
      LOGICAL    L18
      LOGICAL    L19
      LOGICAL    L20
      LOGICAL    L22
      LOGICAL    MARG
      LOGICAL    MIDCYC
      LOGICAL    PNCH
      LOGICAL    SEND
C
C
C
      NFLDB = NUMFIL
      L14 = .FALSE.
      L15 = .FALSE.
      L18 = .FALSE.
      L19 = .FALSE.
      L20 = .FALSE.
      L22 = .FALSE.
      AFOS= .FALSE.
      CARD= .FALSE.
      MARG= .FALSE.
      PNCH= .FALSE.
      SEND= .FALSE.
C
C
      LACAR  = FLAGS(1)
      MIDCYC = FLAGS(2)
      IS1 = 0
      IS2 = 0
      IF(FLAGS(3))IS1 = 1
      IF(FLAGS(4))IS2 = 1
      AFOS   = FLAGS(5)
      SEND   = FLAGS(6)
      CARD   = FLAGS(7)
      MARG   = FLAGS(8)
      PNCH   = FLAGS(9)
C
 10   IF(NFLDB .EQ. 0) GO TO 30
      LFLDB = NFLDB
      ISWTCH = IAND(LFLDB,MASK1)
      IF(ISWTCH .NE. 0) L19 = .TRUE.
      ISWTCH = IAND(LFLDB,MASK2)
      IF(ISWTCH .NE. 0) L14 = .TRUE.
      ISWTCH = IAND(LFLDB,MASK4)
      IF(ISWTCH .NE. 0) L22 = .TRUE.
      ISWTCH = IAND(LFLDB,MASK8)
      IF(ISWTCH .NE. 0) L15 = .TRUE.
      ISWTCH = IAND(LFLDB,MASK16)
      IF(ISWTCH .NE. 0) L18 = .TRUE.
      ISWTCH = IAND(LFLDB,MASK32)
      IF(ISWTCH .NE. 0) L20 = .TRUE.
C
C     FALLING THROUGH HERE MEANS THAT SWITCHES HAVE BEEN DETERMINED FOR
C     EACH FILE.
C
      GO TO 160
C
  30  CONTINUE
C
C          ON THE OTHER HAND, IF THE FILES DESIRED ARE NOT SPECIFIED IN
C     THE PARM FIELD, THEN KRUN CAN BE USED FOR THAT PURPOSE.
C
      IF(KRUN .NE. 5) GO TO 80
C
C     PLOT FT 14, 15, 19, 20, AND 22 ON THE LFM OPTION.
C
      NFLDB = 47
      GO TO 10
C
C     IN EFFECT, I AM JUST SETTING THE PARM SWITCHES HERE.
C
  80  CONTINUE
      IF((KRUN .NE. 8) .AND. (KRUN .NE. 9)) GO TO 100
C
C     1-DOT AND 2-DOT CHARTS GET AIRCFT, SATWND, ACAR, AND ADPUPA.
C
      NFLDB = 39
      IF(MIDCYC) NFLDB = 47
      IF(LACAR) NFLDB = 34
C
C     MODIFICATIONS FOR SPECIAL PURPOSE CHARTS.
C
      GO TO 10
C
 100  CONTINUE
      IF(KRUN .NE. 10) GO TO 110
C
C     FOR TIROS-PLOTTED CHART, ...
C
      NFLDB = 9
      GO TO 10
C
 110  CONTINUE
      IF((KRUN .NE. 11) .AND. (KRUN .NE. 15)) GO TO 120
C
C     THE TROPICAL STRIP AND LARGE NH CHART BOTH TAKE ALL BUT BOGUS.
C
      NFLDB = 47
      GO TO 10
C
 120  CONTINUE
      IF((KRUN .NE. 17) .AND. (KRUN .NE. 18)) GO TO 130
C
C     GENERALIZED NH AND SH PLOTTING GETS IT ALL.
C
      NFLDB = 63
      GO TO 10
C
 130  CONTINUE
      IF(KRUN .NE. 19) GO TO 140
C
C     1:60M NH CHART IS ADP DATA ONLY.
C
      NFLDB = 1
      GO TO 10
C
 140  CONTINUE
      IF((KRUN .NE. 22) .AND. (KRUN .NE. 23)) GO TO 150
C
C     1:26M GOES-TYPE CHARTS -- JUST LIKE TROPICAL OPTION.
C
      NFLDB = 47
      GO TO 10
C
 150  CONTINUE
C
C     MY OVERALL DEFAULT HERE IS TO PLOT ALL BUT BOGUS.
C
      NFLDB = 47
      GO TO 10
 160  RETURN
      END
