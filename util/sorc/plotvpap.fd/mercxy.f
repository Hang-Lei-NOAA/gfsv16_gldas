      SUBROUTINE MERCXY(TLAT,TLONG,TX,TY,IERR)
C                                                      15-AUG-1996/DSS
C     GIVEN...TLAT AND TLONG OF A POINT ( DEGREES WEST LONG FOR TLONG)
C     TO FIND...X AND Y COORDINATES OF THE POINT ON THE TROPICAL GRID
C     ...   (117,44) WHERE PRIME MERIDIAN IS REPEATED...
C     ...ROUNDED TO NEAREST  0.001 DECIMAL FRACTIONAL PART OF GRID INT
C     ...RESTRICTION... GIVEN LATITUDE MUST BE BETWEEN -70.0 AND +70.0
C     ...AND GIVEN LONGITUDE MUST BE BETWEEN 0 AND 360.0 DEGREES WEST

      REAL    CNVDR,PI,ERA,DJGI,EQUATJ
      REAL    GIS,PDEG,GIPDEG,TLONGE
      REAL    TORG
      REAL    DBLAT,DBLONG
      REAL    TLAT,TLONG,TX,TY

      DATA     TLATMX  / 70.0 /
      DATA     CNVDR   / 0.017453292519943 /
C        WHERE CNVDR CONVERTS DEGREES TO RADIANS
      DATA     PI      / 3.1415926536 /
      DATA     EQUATJ  / 19.0 /
C     ...WHERE EQUATJ IS EQUATOR,S SUBSCRIPT FLOATED
      DATA     TORG    / 0.0 /
C     ...WHERE TORG IS DISPLACEMENT OF LHS OF TROPI FROM ZERO LINE
      DATA     GIS,PDEG/ 116.0, 360.0 /
C
      GIPDEG = GIS / PDEG
C     ...WHERE GIPDEG IS GRID INTERVALS PER DEGREE LONG
       TX = 0.0
       TY = EQUATJ
C
      DBLAT = TLAT
      DBLONG = TLONG
      IERR = 1
      IF(TLAT .GT. TLATMX) GO TO 900
      IERR = 2
      IF(TLAT .LT. (-TLATMX)) GO TO 900
      IERR = 3
      IF(TLONG .LT. 0.0) GO TO 900
      IERR = 4
      IF(TLONG .GT. 360.0) GO TO 900
C     ...OTHERWISE, GIVEN LAT/LONG IS WITHIN RANGE
      IERR = 0
C     ... Y COORDINATE IS A FUNCTION OF LATITUDE ONLY
      ERA = TAN((DBLAT * CNVDR / 2.0) + PI / 4.0)
      DJGI   = (GIS / (2.0 * PI)) * LOG(ERA)
      TY = DJGI + EQUATJ
C     ... X-COORDINATE IS A FUNCTION OF LONGITUDE ONLY
      TLONGE = 360.0 - DBLONG
      TX = (TLONGE * GIPDEG) + TORG + 1.0
      RETURN
  900 CONTINUE
C     ...GIVEN LAT OR LONG IS OUT OF RANGE
      RETURN
      END
