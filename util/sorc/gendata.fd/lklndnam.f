      SUBROUTINE LKLNDNAM(CSTNID,LIS_SMQQ,IPRIORITY,IRET_LKN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    LKLNDNAM    FIND PLOT PRIORITY
C   AUTHOR: SHIMOMURA        ORG: W/NP12      DATE: 96-11-18
C
C ABSTRACT: CALLED FROM PRYNAM() IN ORDER TO FIND PLOT PRIORITY 
C   AND SKEW FOR ONE GIVEN SM- OR SA- LAND-STATION NAME
C   FROM LOOK-UP TABLE.
C
C PROGRAM HISTORY LOG:
C   YY-MM-DD  ROBERT JIRSEK; ORIGINAL AUTHOR.
C   91-03-29  R. CHEN; ADD DOC BLOCK
C   96-11-18  SHIMOMURA; CONVERT TO CRAY & TO USE CRAY BLOCK DATA
C
C USAGE:    CALL LKLNDNAM(CSTNID,LIS_SMQQ,IPRIORITY,IRET_LKN)
C   INPUT ARGUMENT LIST:
C     CSTNID   - THE STATION IDENT IN TWO POSSIBLE FORMATS:
C                C*5 CSTNID -- 5-DIGIT BLOCK+STN NUMBER (SM)
C                C*3 CSTNID -- 3-LETTER STATION IDENTIFIER (SA)

C     LIS_SMQQ - LOGICAL SWITCH
C              = .T. IF THE GIVEN CSTNID IS A 5-DIGIT BLOCK+STN NUMBER
C              = .F. IF THE GIVEN CSTNID IS A 3-LETTER STN IDENT
C
C      COMMON /IDTG/  int IYMDT(5)
C                 in which IYMDT(4) is synoptic hour as 2-digit integer
C      COMMON /SMNAM/ MAXNSMS,KSMSTN(960) - LOOK-UP TABLE OF SM
C      COMMON /SANAM/ MAXNSAS,KSASTN(960) - LOOK-UP TABLE OF SA
C
C   OUTPUT ARGUMENT LIST:
C     IPRIORITY - 
C     ISKMX - IN COMMON /ISKEW/ ISKMX 
C                 USED BY SFCPLT.F BY TESTING THE 4 BITS IN ISKMX 
C     IRET_LKN - RETURN CODE
C              =  0;  NORMAL RETURN
C              = -1;  REJECTED THIS STATION DUE TO UNWANTED BLOCK NO.
C              = -2;  REJECTED THIS U.S. BLOCK NO. 72 OR 74 STATION
C                        DUE TO TIME BEING INTERMEDIATE HOUR;
C                        USING HOURLY OBS.
C              = -3;  REJECTED THIS STATION 
C                        DUE TO NO MATCH FOUND IN TABLE
C              =  1;  FAILED.  CSTNID LENGTH(TO LASTCH) .LT. 3 CHAR     
C              =  2;  FAILED.  CSTNID LENGTH(TO LASTCH) .GT. 5 CHAR     
C              =  3;  FAILED.  CSTNID BLK+STN NO.       .LT. 5 DIGITS
C              =  4;  FAILED.  CSTNID BLK+STN NO. WAS NOT 5-DIGITS
C              =  5;  FAILED.  TABLE VALUE OF PRIORITY IS BAD
C              =  6;  FAILED WHILE DECODING TABLE VALUE ATTRIB IN HEX
C              =  7;  FAILED WHILE DECODING BLK NO. OUT OF CSTNID(1:2)
C
C
C   OUTPUT FILES:
C     FT06F001 - PRINT MESSAGES.
C
C REMARKS: LIST CAVEATS, OTHER HELPFUL HINTS OR INFORMATION
C   CHANGED SUBROUTINE NAME FROM "LNDNAM" TO "LKLNDNAM"
C     BECAUSE I CHANGED THE CALL SEQUENCE ...
C     THE OLD CALL SEQUENCE WAS:
C             LNDNAM(I*4  IPRYA,	!... RESULT1 -- PLOT PRIORITY
C                    I*4  STNID1,	!... FIRST 4 CHARACTERS OF IDENT
C                    I*4  STNID2,       !... 5TH CHARACTER OF IDENT
C                    I*4  ITD)		!... =1 FOR SM; =2 FOR SA
C ...      COMMON /ISKEW/ ISKMX         !... RESULT2
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  IBM SP
C
C$$$
C
C ...        MEMBER NAME IS (RJ)NAMLND.
C
C ...      COMMON /SMNAM/ SMSTN,IBKSM(951)
C ...      COMMON /SANAM/ SASTN,IBKSA(919)


      EXTERNAL   LASTCH
      INTEGER    LASTCH		!... INT FUNCTION LASTCH()

      EXTERNAL   BDSMNAM
      COMMON /SMNAM/ MAXNSMS,KSMSTN(960)
      INTEGER MAXNSMS

      EXTERNAL   BDSANAM
      COMMON /SANAM/ MAXNSAS,KSASTN(960)
      INTEGER MAXNSAS

      COMMON /IDTG/  IYMDT(5)		!... IYMDT(4) IS AN INPUT ARG
      COMMON /ISKEW/ ISKMX    		!... ISKMX IS AN OUTPUT ARG

C     . . . . . . . . . . . . . . . . . . . . . . . .
C USAGE:    CALL LKLNDNAM(CSTNID,LIS_SMQQ,IPRIORITY,IRET_LKN)
      CHARACTER*(*) CSTNID
      LOGICAL       LIS_SMQQ
      INTEGER       IPRIORITY
      INTEGER       IRET_LKN
C     . . . . . . . . . . . . . . . . . . . . . . . .


      LOGICAL   LASTLP,LOFFRT,LOFFLW

      INTEGER   NBLOCK(99)
      DATA NBLOCK/3*0,04,3*0,08,61*0,70,71,72,00,74,00,76,00,78,00,80,
     $            19*0/


      DATA MSKNAME       / X'FFFFFFFFFFFF0000' /   	!... 6-CH NAME
      DATA MSKLOWHEX     / Z'0000000F'/


      INTEGER   IWGHT(10)
      DATA      IWGHT    /88,50,52,80,32,26,22,20,00,00/

       INTEGER      ITEMTBL
       CHARACTER*8  CITEMTBL
       EQUIVALENCE (ITEMTBL,CITEMTBL)

       INTEGER      NAMETBL
       CHARACTER*8  CNAMETBL
       EQUIVALENCE (NAMETBL,CNAMETBL)

       INTEGER      NCHGIVNNAM
       INTEGER      NCHNAM
       INTEGER      INTBLOCKNUM

       INTEGER      IHOUR
       LOGICAL      LINTERMEDQ

       INTEGER      KZOOM
       CHARACTER*8  CIDENT	!... LOCAL FOR GIVEN STN ID
       CHARACTER*2  CSM_SA


      SAVE

      IRET_LKN  = 0
      IPRIORITY = 70
      ISKMX = 0
      LENLIMIT = LEN(CSTNID)
      IF(LENLIMIT .LT. 3) THEN
        IRET_LKN = 1
        GO TO 999
      ENDIF
      IF(LENLIMIT .GT. 8) THEN
        LENLIMIT=8
      ENDIF
      NCHGIVNNAM = LASTCH(CSTNID(1:LENLIMIT))
      IF(NCHGIVNNAM .LT. 3) THEN
        IRET_LKN = 1
        GO TO 999
      ELSE IF(NCHGIVNNAM .GT. 5) THEN
        IRET_LKN = 2
        GO TO 999
      ENDIF

      CIDENT(1:) = ' '
      CIDENT(1:NCHGIVNNAM) = CSTNID(1:NCHGIVNNAM)

      IF(LIS_SMQQ) THEN 
        CSM_SA(1:2) = 'SM'
        NCHNAM = 5
        IF(NCHGIVNNAM .LT. NCHNAM) THEN
          IRET_LKN = 3
          GO TO 999
        ENDIF
        GO TO 300

      ELSE
        CSM_SA(1:2) = 'SA'
        NCHNAM = 3
        GO TO 400
      ENDIF
C
C     -----------------------------------------------------------------
C     ... COMES TO 300 IF (LIS_SMQQ) ..................................
 300  CONTINUE
C ... ...SM - SURFACE SYNOPTIC REPORT.
C ... ...STATION TYPE...BY BLOCK AND STATION NUMBER.

C ... ...BLOCK AND STATION NUMBER CHECK...
      DO  IC = 1,5
       IF((CIDENT(IC:IC) .LT. '0') .OR. (CIDENT(IC:IC) .GT. '9'))THEN
          WRITE(6,FMT='(1H ,''LKLNDNAM: BLOCK OR STATION NUMBER IS '',
     1                    ''OUT-OF-RANGE.'',
     2            /1H ,''   BLOCK NUM ='',A2,''   STATION NUM ='',A3)')
     A          CIDENT(1:2),CIDENT(3:5)
          IRET_LKN = 4
          GO TO 999
        ENDIF
      ENDDO


      IHOUR = IYMDT(4)
      LINTERMEDQ = .FALSE.
      IF(IHOUR .EQ. 3 .OR. IHOUR .EQ. 9 .OR. 
     2   IHOUR .EQ. 15 .OR. IHOUR .EQ. 21) THEN
        LINTERMEDQ = .TRUE.
      ENDIF

C     ... TO CONVERT 2-DIGIT BLOCK NUMBER FROM ASCII TO BINARY INTEGER,

      READ( UNIT=CIDENT(1:2), FMT=325, ERR=920 ) INTBLOCKNUM
  325 FORMAT(I2)

C     IF(NBLOCK(INTBLOCKNUM) .EQ. 0) THEN
C       IRET_LKN = -1		!... NOT WANTED BLOCK NUM IN THIS APP
C       GO TO 999
C     ENDIF

C     ... IF INTERMEDIATE HOUR, SKIP UNITED STATES SI REPORTS. 
C     ...    USE SA REPORTS INSTEAD.
      IF((INTBLOCKNUM .EQ. 72  .OR. INTBLOCKNUM .EQ. 74) .AND.
     1   (LINTERMEDQ)) THEN
          IRET_LKN = -2 	!... NOT WANTED SI AT INTERMEDIATE TIME 
          GO TO 999
      ENDIF
C

C ... ...
      MXTBL =MAXNSMS
      IPRIORITY =70
      GO TO 500
C
C     -----------------------------------------------------------------
C     ... COMES TO 400 IF(.NOT. LIS_SMQQ) .............................
 400  CONTINUE
C ... ...SA - SURFACE HOURLY REPORT.
C ... ...THREE LETTERS ID .
C ...      ID =ISHFT(IDSTN1,-8)
      IPRIORITY =4
      MXTBL =MAXNSAS
      GO TO 500

C     -----------------------------------------------------------------
C     ...     TO PERFORM A TABLE LOOK-UP FOR THE STATION NAME ...

 500  CONTINUE
C ... ...TABLE AND INPUT STATION NAMES ARE IN INCREASING ORDER.
      MIDPT =MXTBL/2 +1
      INCR =MIDPT
      LASTLP =.FALSE.
      LOFFRT =.FALSE.
      LOFFLW =.FALSE.
C ... ...START OF BINARY SEARCH LOOP...
 200  CONTINUE
      IF(INCR.LE.1) LASTLP =.TRUE.
C ... ...OTHERWISE, FINAL STEP IS NOT YET REACHED.
      INCR =(INCR +1)/2
      IF(LOFFRT) GO TO 210
      IF(LOFFLW) GO TO 220
      MDIS =MIDPT
      IF(LIS_SMQQ) THEN
        ITEMTBL =KSMSTN(MDIS)
      ELSE
        ITEMTBL =KSASTN(MDIS)
      ENDIF
      NAMETBL =IAND(ITEMTBL,MSKNAME)
C ...      IF(ID -ITBL) 210,240,220
      IF(CIDENT(1:NCHNAM) .LT. CNAMETBL(1:NCHNAM)) THEN
        GO TO 210
      ELSE IF(CIDENT(1:NCHNAM) .EQ. CNAMETBL(1:NCHNAM)) THEN
        GO TO 240  		!... FOUND THE MATCH ...
      ELSE IF(CIDENT(1:NCHNAM) .GT. CNAMETBL(1:NCHNAM)) THEN
        GO TO 220
      ENDIF

 210  CONTINUE
      LOFFRT =.FALSE.
C ... ...GO TO LOWER HALF AND BISECT TABLE.
      IF(LASTLP) GO TO 280
      MIDPT =MIDPT -INCR
      IF(MIDPT.GT.0) GO TO 200
C ... ...OTHERWISE, SEARCH FELL BELOW LOWER END OF TABLE...
C ... ...SET SWITCH LOFFLW AND LET IT JUMP BACK INTO TABLE
      LOFFLW =.TRUE.
      GO TO 200

 220  CONTINUE
      LOFFLW =.FALSE.
C ... ...GO TO UPPER HALF AND BISECT TABLE.
      IF(LASTLP) GO TO 280
      MIDPT =MIDPT +INCR
      IF(MIDPT.LE.MXTBL) GO TO 200
C ... ...OTHERWISE, SEARCH FELL BELOW LOWER END OF TABLE...
C ... ...SET SWITCH LOFFRT AND LET IT JUMP BACK INTO TABLE...
      LOFFRT =.TRUE.
      GO TO 200

 240  CONTINUE
C ... ...FOUND IT...

      READ( UNIT=CITEMTBL(7:8), FMT=245, ERR=910 ) KZOOM
  245 FORMAT(Z2)

      IPRIY =IAND(KZOOM,MSKLOWHEX)
      ISKMX = ISHFT(KZOOM,-4)

      IF(IPRIY.LT.1 .OR. IPRIY.GT.8) THEN
        PRINT 2700,CITEMTBL(1:8),IPRIY
 2700   FORMAT(' ','lklndnam: ERROR:FOUND STATION= "',A,
     1             '" CONTAINS BAD PRIORITY VALUE=',I4)
        IRET_LKN = 5
        GO TO 999
      ENDIF

      IPRIORITY =IWGHT(IPRIY)
      GO TO 999


C ... ...COMES  HERE IF NO SUCH ID FOUND IN TABLE.
 280  CONTINUE
      IRET_LKN = -3		!... BINARY SEARCH FAILED TO FIND IN TBL
      GO TO 999

  910  CONTINUE
       WRITE(6,915)CSM_SA,CITEMTBL(1:8)
  915  FORMAT(1H ,'LKLNKNAM:ERROR WHILE DECODING AFOS ATTRIBUTE FROM',
     1       /1H ,A2,'-TABLE ITEM=',A8)
       IRET_LKN = 6
       GO TO 999

  920  CONTINUE
       WRITE(6,925)CSM_SA,CIDENT(1:5)
  925  FORMAT(1H ,'LKLNKNAM:ERROR WHILE DECODING BLOCK-NUMBER FROM',
     1       /1H ,A2,'-GIVEN BLK+STN NUMBER=',A5)
       IRET_LKN = 7
       GO TO 999

  999  CONTINUE
       RETURN
       END
