      PROGRAM GRIDBUL
C$$$  MAIN PROGRAM DOCUMENTATION BLOCK  ***
C
C MAIN PROGRAM: BULLS_GRIDBULL
C   PRGMMR: GILBERT          ORG: NP11        DATE: 2000-01-06
C
C ABSTRACT: THIS PROGRAM FORMATS GRID-POINT VALUES IN FM 47-V GRID CODE
C   FOR TRANSMISSION ON THE GLOBAL TELECOMMUNICATIONS SYSTEM (GTS).  AN
C   EXTERNAL SEQUENTIAL DATA SET CONTAINING ONE OR MORE TWO CARD SUBSETS
C   CONTROLS THE BULLETINS PRODUCED.  EACH SUBSET PROVIDES ELEMENTS 5-7
C   OF GRIB PDS INPUT FIELD IDENTIFICATION, A DIRECT ACCESS FILE
C   DDNAME, A BULLETIN SERIES NAME, AND THE CCAP MDL CATALOG NUMBER(S).
C   A NONZERO MDL NUMBER TELLS THE PROGRAM TO PRODUCE THE CORRESPONDING
C   BULLETIN IN THE SERIES OF TWELVE.  OFFICE NOTE 167 DESCRIBES FULLY
C   THE TWELVE BULLETIN AREAS, THE BULLETIN NAMING CONVENTIONS, THE
C   TYPES OF DATA, AND THE BULLETIN FORMATS.
C
C PROGRAM HISTORY LOG:
C   81-12-20  R. ALLARD, ORIGINAL AUTHOR for NAS version
C
C   96- 6-21  W. Bostelman, Converted pgm and subroutines to run on CRAY
C             Input data is now from sequential GRIB files.  
C             The MM parameter table has been updated to reflect current
C             models and procedures.
C   97- 2-10  W. Bostelman, Corrected Line Feed Definition
C   97- 2-26  W. Bostelman, Added optional Grid Type Parameter to input
C             cards and upgraded the processing summary and diagnostic
C             messages.  Input file error messages will now contain the
C             offending file name and summaries will indicate how much
C             processing was done with respect to total requested.
C   98-06-29  Gilbert    - Removed calls to W3LOG.
C 2012-11-02  VUONG      CHANGED VARIABLE ENVVAR TO CHARACTER*6
C 2016-02-19  VUONG      SET JGDS=-1 TO SEARCH AS A WILDCARD
C
C USAGE:
C   INPUT FILES:
C
C     FT20F001 REFERENCES A SEQUENTIAL FILE THAT CONTROLS VIA TWO CARD
C              SUBSETS THE SEQUENTIAL FILE(S) AND DATA FIELD(S) READ
C              AND THE GRID BULLETIN(S) PRODUCED.  CARD ONE CONTAINS A
C              DIRECT ACCESS INPUT FILE DDNAME LEFT-ADJUSTED IN COLUMNS
C              1-8, A THREE WORD GRID PDS LABEL IN 
C              COLUMNS 11-38 WITH TWO SPACES SEPARATING EACH HEXADECIMAL
C              WORD, THE BULLETIN SERIES NAME IN GENERIC FORM TTAAII IN
C              COLUMNS 63-68, AND A "1" IN COLUMN 71.  CARD TWO CONTAINS
C              TWELVE RIGHT-ADJUSTED FIVE DIGIT MDL NUMBERS IN COLUMNS
C              1-60, THE BULLETIN SERIES NAME REPEATED IN COLUMNS 63-68,
C              AND A "2" IN COLUMN 71.
C
C     GRIB data files start at ft11
C     GRIB index file   "    " ft31
C     They correspond to the old file names given on the data cards.
C        (EXAMPLES: ANL, F12, F24, F36, F48, F72, FXN, GANL, GF12).
C
C   OUTPUT FILES:
C
C     FT06F001 - I/O SUMMARY AND ANY ERROR MESSAGES FOR THE PRINTER.
C
C     FT88F001 - SEQUENTIAL FILE CONTAINING GRID BULLETIN(S) IN OFFICE
C              NOTE 167 FORMAT AND BLOCKED UNDER OFFICE NOTE 100
C              GUIDELINES.
C
C     JOBLOG   - REQUIREMENT WHEN JOBNAME IN PRODUCTION FORMAT.
C
C
C   SUBPROGRAMS CALLED:
C
C     UNIQUE:  BIN2CHAR, BIN2EBCD, BULLET, PAKWND, QUADAX, WINDY 
C
C     W3LIB:   W3TAGB, W3TAGE
C
C   EXIT STATE:
C
C     COND = 0
C
C   REMARKS:
C
C     1.  THE SERIES BULLETIN NAME CORRESPONDS TO THE FIRST QUADRANT
C     AREA OF THE NORTHERN HEMISPHERE.  THE PROGRAM MODIFIES THE THIRD
C     LETTER OF THE NAME WITH THE LETTER A TO L CORRESPONDING TO THE
C     MDL NUMBER POSITION IN THE DATA CARD.  A ZERO MDL NUMBER MEANS
C     TO SKIP THE CORRESPONDING BULLETIN.
C
C     2.  THE THREE WORD PDS label IN THE FIRST CARD OF A TWO CARD
C     SUBSET ALWAYS DENOTES A GLOBAL FIELD. 
C
C     3.  THE U-COMPONENT IDENTIFIER ALWAYS APPEARS IN THE FIRST CARD
C     FOR WIND BULLETINS.  THE PROGRAM MODIFIES THE Q-IDENTIFIER TO
C     INPUT THE V-COMPONENT.
C
C     4.  THE PROGRAM HANDLES ONLY  GRIB data on GRID type 3.
C
C     5.  WHEN THE PROGRAM RECOGNIZES AN ERROR CONDITION, IT PRINTS A
C     MESSAGE AND SKIPS THE CURRENT TWO CARD SUBSET.
C
C     6.  PROGRAM FORMS HEIGHT, TEMPERATURE, WIND, RELATIVE HUMIDITY,
C     AND PRESSURE BULLETINS.
C
C     ***************************************************************
C
C ATTRIBUTES:
C
C   LANGUAGE:  FORTRAN 77
C
C   SOURCE STATEMENTS:                  PGM SIZE:
C
C$$$
C
      PARAMETER    (MXSIZE = 65160,lrecl=1280)
C
      CHARACTER*1  LTRS (26)
      CHARACTER*1  LNFEED
      CHARACTER*1  TTAAII (6)
      CHARACTER*2  II (17)
      CHARACTER*2  KXXT, LXXT
      CHARACTER*2  NAME (3)
      CHARACTER*2  NTYPE (6)
      CHARACTER*4  TBULL, TXBULL
      CHARACTER*8  DDPOST, KEYWD
      CHARACTER*8  DDNAME, PRNAME, GNAME1, GNAME2,PRINIT
      CHARACTER*6  envvar
      CHARACTER*44 MSG
      CHARACTER*80 fileb,filei,fileo
      CHARACTER*2560 NBLK
C
      INTEGER      IDTBL (1539)
      INTEGER      IFLD (2695)
      INTEGER      IUNITS (10)
      INTEGER      KTYPE (12)
      INTEGER      LEVS (16)
      INTEGER      LOCTBL (256)
      INTEGER      MDL (12)
      INTEGER      MPDS (27), KPDS (27)
      INTEGER      JGDS (100), KGDS (100)
      INTEGER      KTAU (19),  NTAU
      INTEGER      KDIR (285), KSPD (285),  KFLD (285)
C
      REAL         ADDKH (15)
      REAL         FLDA (285),    FLDB (285)
      REAL         FLD1 (MXSIZE), FLD2 (MXSIZE)
C
      LOGICAL*1      KBMS (MXSIZE)
C
      EQUIVALENCE  (GNAME1, NAME(1), TTAAII(1))
C
      DATA         KMAX  / 255 /
      DATA         KNBR  / 285 /
      DATA         MSG   /'***      of      GRID BULLETINS GENERATED.:'/
C
C DATA SET REFERENCE NUMBERS USED FOR SEQUENTIAL FILES.
C
      DATA         KARD / 20 /
      DATA         KOUT / 88 /
      DATA         KPRT /  6 /
      DATA         KPST /  5 /
C
C TABLE OF CONSTANTS SUBTRACTED FROM HEIGHT DATA TO PRESERVE 3 DIGITS.
C
      DATA    ADDKH /
C           .....   1000 MB    850 MB    700 MB    500 MB    400 MB
     &              -100.0,      0.0,      0.0,      0.0,      0.0,
C           ......   300 MB     250 MB   200 MB    150 MB    100 MB
     &               700.0,    800.0,    900.0,   1000.0,   1000.0,
C           .......   70 MB     50 MB     30 MB     20 MB     10 MB
     &              1500.0,   1500.0,   2000.0,   2000.0,   2500.0  /
C
C TABLE OF PERMISSIBLE TT CHARACTERS IN GNAMES.  (TTAAII)
C                                                       (INDEX = JTYPE)
      DATA NTYPE / 'GP','GH','GT','GW','GR','GU' /
C
C TABLE OF LEVEL DESIGNATORS PERMITTED IN THE GNAMES.  (TTAAII)
C
      DATA II / '98', '99', '85', '70', '50',
     A          '40', '30', '25', '20', '15',
     B          '10', '07', '05', '03', '02',
     C          '01', '97' /
C
C TABLE OF PERMISSIBLE FIRST ID WORDS (SANS TAU) IN O.N. 84 FORMAT.
C                                                       (INDEX = KIND)
      DATA KTYPE /  7, 52, 11, 11, 11, 11, 33, 34, 1, 2, 1, 80 /
C                HEIGHT
C                RELATIVE HUMIDITY
C                TEMPERATURE     (SFC)       (MSL)      (TROP)
C                WIND             (TROP)
C                PRESSURE(SFC)    (MSL)      (TROP)
C                WTMP
C
C TABLE OF PERMISSIBLE LEVELS
C
      DATA LEVS /     0,    1000,       850,       700,       500,
     &              400,     300,       250,       200,       150,
     &              100,      70,        50,        30,        20,
     &               10  /
C
C TABLE OF PERMISSIBLE TAUS (FORECAST HOURS).
C
      DATA KTAU /
     A   0,6,12,18,24,30,36,42,48,60,72,84,96,108,120,132,144,156,168 /
C
C TABLE OF ALPHABETIC CHARACTERS.
C
      DATA LTRS / 'A','B','C','D','E','F','G','H','I','J','K','L','M',
     A            'N','O','P','Q','R','S','T','U','V','W','X','Y','Z' /
C
      DATA IUNITS / 11, 31,  12, 32,  13, 33,  14, 34,  15, 35  /
C
C     DATA JGDS  / 0, 360, 180, 89500, 500, 128, -89500, -500, 2*1000,
C    &             9*0, 255, 80*0 /
C
   10 FORMAT(A8,5I10,4X,A8,I1,/,12I5,2X,A8,I1)
   20 FORMAT('0ERROR READING DATA CARDS IN SUBSET NUMBER ',I2,'.')
   40 FORMAT('0CARD SUBSET ',I2,' LABELS DO NOT MATCH: ',A8,2X,A8)
   50 FORMAT('0CARD SUBSET',I2,' HAS BAD SEQUENCE NUMBERS: ',2(2X,I5))
   80 FORMAT('0BAD GNAME IN LABEL OF DATA CARD SUBSET ',I5,'.')
   90 FORMAT('0TAU VALUE ',I5,' IS NOT VALID IN CARD SUBSET ',I5,'.')
  100 FORMAT('0LEVEL ',Z8,' NOT IN TABLE.  CARD SUBSET ',I5,'.')
C
C ....  INITIALIZATION.
C
      CALL W3TAGB('BULLS_GRIDBULL',2000,0006,0098,'NP11') 

      IU     = 0
      IBULLS = 0
      NAREAS = 12
      NBULLS = 0
      KRDSET = 0
      LNFEED = CHAR(10)
      NBRTXS = 0
      PRINIT = 'PRNAME'
      PRNAME = PRINIT
      KXXT='XXXX'
      LXXT='XXXX'
c
c       open output transmission file
c
      ENVVAR='FORT  '
      WRITE(ENVVAR(5:6),FMT='(I2)') KOUT
      CALL GETENV(ENVVAR,FILEO)
      OPEN(KOUT,FILE=FILEO,ACCESS='DIRECT',RECL=lrecl)
      irec=1
C
C ....  READ A TWO CARD SUBSET TO INITIALIZE INPUT-OUTPUT PARAMETERS.
C
 1000 CONTINUE
      JREW  =   0
      JGDS  =  -1
      MPDS  =  -1
      READ (KARD,10,ERR=7000,END=6000) DDNAME,(MPDS(I),I=5,7),IGRD
     &     ,IDUM ,GNAME1, KSEQ1,MDL,GNAME2,KSEQ2
C
      IF (IGRD .EQ. 0) THEN  ! Check for Grid Type
        MPDS(3) = 3  ! Default
      ELSE
        MPDS(3) = IGRD
      ENDIF
C
C ....  Accumulate Bulletin Total to Make
C
      DO IAREA = 1, NAREAS
        IF (MDL(IAREA) .GT. 0) THEN
          IBULLS = IBULLS + 1
        ENDIF
      END DO
C
      KRDSET=KRDSET+1
C ......  CHECK THE CARD LABELS AND SEQUENCE NUMBERS.
      IF(GNAME1.NE.GNAME2) GO TO 7100
      IF(KSEQ1.NE.1.OR.KSEQ2.NE.2) GO TO 7200
C ......  DETERMINE IF NEW FILE, FIRST FILE OR SAME AS PREVIOUS FILE.
      IF(DDNAME.EQ.PRNAME) GO TO 1150
      IF(PRNAME.EQ.PRINIT) GO TO 1100
C ......  CLOSE PREVIOUS FILE.
      CALL BACLOSE (LUGB,irt)
      CALL BACLOSE (LUGI,irt)
 1100 CONTINUE
      IU = IU + 1
      LUGB = IUNITS(IU)
      IU = IU + 1
      LUGI = IUNITS(IU)
      PRNAME=DDNAME
C
C ....  Open GRIB data file and index file
C
      write(envvar(5:6),fmt='(I2)') lugb
      call getenv(envvar,fileb)
      write(envvar(5:6),fmt='(I2)') lugi
      call getenv(envvar,filei)
      call baopenr(lugb,fileb,iret1)
      call baopenr(lugi,filei,iret2)

      if ( iret1+iret2 .ne. 0 ) then
        write(6,*)' Error opening GRIB data or index file.'
        stop 2
      endif

 1150 CONTINUE
C
C  ............ R E A D    I N    A N D   U N P A C K   G R I B
C                D A T A   F I E L D
C
      CALL GETGB (LUGB, LUGI, MXSIZE, JREW, MPDS, JGDS, 
     &             KPTS, KREW, KPDS, KGDS, KBMS, FLD1, IRET)
      IF (IRET .NE. 0) THEN
        PRINT *,  'Data field not found Reading Unit ', LUGI,', IRET = '
     &         ,IRET,', MPDS ids = ',MPDS(5), MPDS(6), MPDS(7)
        GO TO 1000
      ENDIF
C
C  ..... SET DATA TYPE INDICATOR.
      KIND=0
      DO 1300 K=1,12
      KIND=K
      KXX=MPDS(5)
      IF(KXX.EQ.KTYPE(K)) GO TO 1350
 1300 CONTINUE
      PRINT *, ' Data type ',MPDS(5),' is not recognized by table'
      GO TO 1000
C
 1350 CONTINUE
      KWND=0
      IF(KIND.LT.7.OR.KIND.GT.8) GO TO 1500
      KWND=1
      MPDS(5) = MPDS(5) + 1
C
C  ............ R E A D    I N    A N D   U N P A C K   G R I B
C                D A T A   F I E L D
C
      CALL GETGB (LUGB, LUGI, MXSIZE, JREW, MPDS, JGDS, 
     &             KPTS, KREW, KPDS, KGDS, KBMS, FLD2, IRET)
      IF (IRET .NE. 0) THEN
        PRINT *,  'Data field not found Reading Unit ', LUGI,', IRET = '
     &         ,IRET,', MPDS ids = ',MPDS(5), MPDS(6), MPDS(7)
        GO TO 1000
      ENDIF
C
 1500 CONTINUE
C .....  CHECK FOR LETTER G AND TYPE INDICATOR IN GNAME LABEL.
      DO 1600 J=1,6
      JTYPE=J
      IF(NAME(1).EQ.NTYPE(J)) GO TO 1700
 1600 CONTINUE
      GO TO 7500
C
 1700 CONTINUE
C ..... VERIFY THAT LETTER A IS SPECIFIED IN AREA DESIGNATOR OF GNAME
C ..... LABEL.
      KXXT=LTRS(1)
      LXXT=TTAAII(3)
      IF(LXXT.NE.KXXT) GO TO 7500
C ..... VERIFY THE TAU PORTION OF ID(1) AND THE GNAME LABEL.
      NTAU = KPDS(14)
      DO 2100 J=1,19
      KXXT=LTRS(J)
      IF(NTAU.EQ.KTAU(J)) GO TO 2200
 2100 CONTINUE
      GO TO 7600
C
 2200 CONTINUE
      LXXT=TTAAII(4)
      IF(LXXT.NE.KXXT) GO TO 7500
C ..... VERIFY THE LEVEL PORTION (II) OF THE GNAME LABEL.
      JLEV=0
      IF(KPDS(7).NE.0) GO TO 2300
      IF(NAME(3).EQ.II(1)) JLEV=1
      IF(NAME(3).EQ.II(17)) JLEV=17
      IF(JLEV.NE.0) GO TO 2600
      GO TO 7500
C
 2300 CONTINUE
      DO 2400 J=2,16
      JLEV=J
      IF(KPDS(7).EQ.LEVS(J)) GO TO 2500
 2400 CONTINUE
      GO TO 7700
 2500 CONTINUE
      IF(NAME(3).NE.II(JLEV)) GO TO 7500
 2600 CONTINUE
      IF(JTYPE.GT.3) GO TO 3500
C ..... INITIALIZE CONSTANTS TO ADJUST DATA FROM O.N. 84 TO GRID CODE
C ..... UNITS.
      IF(JTYPE.NE.3) GO TO 3000
C     ...TEMPERATURE. (KELVIN TO CELSIUS)
      SCALE=1.0
      ADDON=273.16
      GO TO 3200
C
C     ...HEIGHT. (GPM TO DECAMETERS)
 3000 CONTINUE
      IF(JTYPE.NE.2) GO TO 3100
      SCALE=0.1
      ADDON=ADDKH(JLEV-1)
      GO TO 3200
C
 3100 CONTINUE
      IF(KIND.LT.9.OR.KIND.GT.10) GO TO 3500
C     ...PRESSURE. (SFC OR MSL)
      SCALE = 0.01
      ADDON = 200.0
 3200 CONTINUE
C ..... RESCALE DATA FIELD(S). (SEE TABLES I AND II IN O.N. 167)
      DO 3300 K=1,KPTS
      FLD1(K) = FLD1(K) * SCALE - ADDON
 3300 CONTINUE
      IF (JTYPE .EQ. 3 .AND. JLEV.EQ.1) THEN  ! Add -90.5 row for SST
         DO K = 1, 360
           FLD1 (KPTS + K) = FLD1 (KPTS - 360 + K)
         END DO
      ENDIF
 3500 CONTINUE
C
C ..... DETERMINE WHICH OF THE 12 GRID AREAS ARE BULLETINS.  ADJUST 
C ..... BULLETIN NAME TO CORRECT AREA (A-L).  DETERMINE WHETHER 
C ..... NORTHERN HEMISPHERE, TROPICAL BELT, OR SOUTHERN HEMISPHERE
C ..... BULLETIN.   
C
      DO 5700 M=1,12
      IF(MDL(M).EQ.0) GO TO 5700
      TTAAII(3)=LTRS(M)
C ..... EXTRACT QUADRANTS FROM A 1 X 1 DEGREE GLOBAL GRID.
      N=M
      CALL QUADAX (FLD1, FLDA, N)
C
      IF(KWND.EQ.0) GO TO 5000
      CALL QUADAX (FLD2, FLDB, N)  !  V-WIND COMPONENT
C
      IF(JTYPE.EQ.6) GO TO 4700
C ..... FORM WIND DIRECTION AND SPEED.
      CALL WINDY(FLDA,FLDB,KDIR,KSPD,KNBR)
C ..... SET WIND DIRECTION TO NEAREST 5 DEGREES AND FORM WIND GROUP 
C ..... DDDVV.
      DO 4600 K=1,KNBR
      KDIR(K)=(FLOAT(KDIR(K))+2.5)/5.0
      KDIR(K)=KDIR(K)*5
      IF(KDIR(K).EQ.0.AND.KSPD(K).GT.0) KDIR(K)=360
      KFLD(K)=(KDIR(K)*100)+KSPD(K)
 4600 CONTINUE
      GO TO 5500
C
 4700 CONTINUE
C ..... PACK U AND V WIND COMPONENTS INTO SUUVV WIND GROUP.
      CALL PAKWND(FLDA,FLDB,KFLD,KNBR)
      GO TO 5500
C
 5000 CONTINUE
C ..... CONVERT QUADRANT DATA TO INTEGER.
      DO 5100 K=1,KNBR
      IF(FLDA(K).LT.0) GO TO 5050
      KFLD(K)=FLDA(K)+0.5
      GO TO 5100
C
 5050 CONTINUE
      KFLD(K)=FLDA(K)-0.5
 5100 CONTINUE
      IF(JTYPE.NE.3) GO TO 5300
C ..... ADJUST TEMPERATURES 500 MB AND ABOVE.
C ..... POSITIVE VALUES ARE ZEROED AND NEGATIVE VALUES ARE ABSOLUTE.
      IF(JLEV.LT.5) GO TO 5500
      DO 5200 K=1,KNBR
      IF(KFLD(K).GT.0) KFLD(K)=0
      KFLD(K)=IABS(KFLD(K))
 5200 CONTINUE
 5300 CONTINUE
      IF(JTYPE.NE.5) GO TO 5500
C ..... SET RELATIVE HUMIDITY VALUES > THAN 99 PERCENT TO 99.
      DO 5400 K=1,KNBR
      IF(KFLD(K).GT.99) KFLD(K)=99
 5400 CONTINUE
 5500 CONTINUE
C
C ..... FORMAT AND OUTPUT A BULLETIN.
C
      CALL BULLET(GNAME1,MDL(M),KFLD,KPDS,NEXT,NBLK)
      NBRTXS=NBRTXS+1
      WRITE (KOUT,rec=irec) NBLK(:1280)
c      WRITE (KOUT,rec=irec) NBLK(:1280), LNFEED
      irec=irec+1
      IF(NEXT.LE.1280) GO TO 5700
      WRITE (KOUT,rec=irec) NBLK(1281:)
c      WRITE (KOUT,rec=irec) NBLK(1281:), LNFEED
      irec=irec+1
 5700 CONTINUE
      GO TO 1000
C
C ..... PRINT THE RUN SUMMARY AND EXIT THE PROGRAM.
 6000 CONTINUE
C
      NBULLS = IBULLS
      NUNPRO = NBULLS - NBRTXS
      WRITE(KPRT,*)
      WRITE(KPRT,*) ' NO. OF DATA CARDS PROCESSED ...... ', KRDSET
      WRITE(KPRT,*) ' NO. OF BULLETINS TO MAKE ......... ', NBULLS
      WRITE(KPRT,*) ' NO. OF BULLETINS IN TRANS FILE ... ', NBRTXS
      WRITE(KPRT,*) ' NO. OF UNPROCESSED BULLETINS ..... ', NUNPRO
      WRITE (TBULL, 15) NBRTXS
      WRITE (TXBULL, 15) NBULLS
   15 FORMAT (I4)
      MSG(5:8)   = TBULL
      MSG(13:16) = TXBULL
      CALL W3TAGE('BULLS_GRIDBULL') 
      STOP
C
C ..... ERROR READING A DATA CARD SUBSET.  SUBSET SKIPPED.
 7000 CONTINUE
      KRDSET=KRDSET+1
      WRITE (KPRT,20) KRDSET
      GO TO 1000
C
C ..... BAD DATA CARD SUBSET LABEL(S).  SUBSET SKIPPED.
 7100 CONTINUE
      WRITE (KPRT,40) KRDSET,GNAME1,GNAME2
      GO TO 1000
C
C ..... BAD DATA CARD SUBSET SEQUENCE NUMBER(S).  SUBSET SKIPPED.
 7200 CONTINUE
      WRITE(KPRT,50) KRDSET,KSEQ1,KSEQ2
      GO TO 1000
C
C ..... BAD GNAME LABEL IN CARD SUBSET.  SUBSET SKIPPED.
 7500 CONTINUE
      WRITE (KPRT,80) KRDSET
      GO TO 1000
C
C ..... BAD TAU IN ID(1) OF CARD SUBSET.  SUBSET SKIPPED.
 7600 CONTINUE
      WRITE (KPRT,90) NTAU,KRDSET
      GO TO 1000
C
C ..... ILLEGAL LEVEL IN ID(2) OF CARD SUBSET.  SUBSET SKIPPED.
 7700 CONTINUE
      WRITE (KPRT,100) KPDS(7), KRDSET
      GO TO 1000
C
      END
